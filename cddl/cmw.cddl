cmw = [ type, value ]

type = coap-content-format / media-type

coap-content-format = uint .size 2
media-type = text .abnf ("media-type" .det RFC6838)

value = cbor-bytes / base64-string

cbor-bytes = bytes
base64-string = text .regexp "[A-Za-z0-9_=-]+"

RFC6838 = '
media-type = type-name "/" subtype-name *1("+" suffix) parameters

type-name = restricted-name
subtype-name = restricted-name

; see https://www.iana.org/assignments/media-type-structured-suffix/
suffix = "xml" / "json" / "ber" / "cbor" / "der" / "fastinfoset" /
         "wbxml" / "zip" / "tlv" / "json-seq" / "sqlite3" / "jwt" /
         "gzip" / "cbor-seq" / "zstd"

parameters = *(";" parameter-name "=" parameter-value)

parameter-name = restricted-name
parameter-value = *VCHAR

restricted-name = restricted-name-first *126restricted-name-chars
restricted-name-first  = ALPHA / DIGIT
restricted-name-chars  = ALPHA / DIGIT / "!" / "#" / "$" / "&" / "-" /
                         "^" / "_"
restricted-name-chars =/ "." ; Characters before first dot always
                             ; specify a facet name
restricted-name-chars =/ "+" ; Characters after last plus always
                             ; specify a structured syntax suffix

VCHAR = %x21-7E            ; Visible (printing) characters
ALPHA = %x41-5A / %x61-7A  ; A-Z / a-z
DIGIT = %x30-39            ; 0-9
'
